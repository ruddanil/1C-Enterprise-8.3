// { + Рудь Д.А. Ведомость выдачи молока	
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ВедомостьВыдачиМолока") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ВедомостьВыдачиМолока", "Ведомость выдачи молока и пищевых продуктов (ПУЗ.29)",
			ПечатьВедомостьВыдачиМолока(МассивОбъектов, ОбъектыПечати, ПараметрыПечати), , "Документ.ТребованиеНакладная.ПФ_MXL_ВедомостьВыдачиМолока");		
// }

// { + Рудь Д.А. Ведомость выдачи молока
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ВедомостьВыдачиМолока";
	КомандаПечати.Представление = НСтр("ru = 'Ведомость выдачи молока и пищевых продуктов (ПУЗ.29)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок       = 40;
// }

// { + Рудь Д.А.
Функция ТекстЗапросаВедомостьВыдачиМолока()
		
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТребованиеНакладная.Ссылка КАК Ссылка,
	|	ТребованиеНакладная.Организация КАК Организация,
	|	ТребованиеНакладная.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ТребованиеНакладная.Сотрудник.ФИО КАК СотрудникФИО,
	|	ТребованиеНакладнаяМатериалы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТребованиеНакладнаяМатериалы.Количество КАК Количество,
	|	ТребованиеНакладнаяМатериалы.Номенклатура КАК Номенклатура
	|ИЗ
	|	Документ.ТребованиеНакладная КАК ТребованиеНакладная
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТребованиеНакладная.Материалы КАК ТребованиеНакладнаяМатериалы
	|		ПО ТребованиеНакладная.Ссылка = ТребованиеНакладнаяМатериалы.Ссылка
	|ГДЕ
	|	ТребованиеНакладная.ВидОперации.Ссылка = &ВидОперации
	|	И ТребованиеНакладная.Материалы.Номенклатура.Наименование = ""Молоко и Молочные продукты""
	|	И ТребованиеНакладная.Ссылка В(&МассивОбъектов)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПечатьВедомостьВыдачиМолока(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб		   = Истина;
	ТабличныйДокумент.ОриентацияСтраницы   = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ВедомостьВыдачиМолока";
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ВидОперации", Перечисления.ВидыОперацийРасходМатериалов.ПередачаСотруднику);
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = ТекстЗапросаВедомостьВыдачиМолока();
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Нет данных для формирования печатной формы";
		Сообщение.Сообщить();
		
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	// Определение количества строк в документах
	КоличествоСтрокДокумента = Новый Соответствие;
	Пока Выборка.СледующийПоЗначениюПоля("Ссылка") Цикл
		КоличествоСтрок = 0;
		Пока Выборка.Следующий() Цикл
			Если Выборка.Номенклатура <> Null Тогда 
				КоличествоСтрок = КоличествоСтрок + 1;
			КонецЕсли;
		КонецЦикла;
		КоличествоСтрокДокумента.Вставить(Выборка.Ссылка, КоличествоСтрок);
	КонецЦикла;
	Выборка.Сбросить();
	
	ПервыйДокумент = Истина;
	
	Пока Выборка.СледующийПоЗначениюПоля("Ссылка") Цикл
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ТребованиеНакладная.ПФ_MXL_ВедомостьВыдачиМолока");
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Вывод шапки
		ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Выборка.Организация);
		ОбластьШапка.Параметры.Организация = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм");
		ОбластьШапка.Параметры.ПодразделениеОрганизации = Выборка.ПодразделениеОрганизации;
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		// Вывод наименования и измерений
		ОбластьТаблицы = Макет.ПолучитьОбласть("Таблица");
		ОбластьТаблицы.Параметры.Заполнить(Выборка);
		ТабличныйДокумент.Вывести(ОбластьТаблицы);
		
		ТекущаяСтрока = 0;
		КоличествоСтрок = КоличествоСтрокДокумента.Получить(Выборка.Ссылка);
		ИтогоКоличество = 0;
		
		ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
		ОбластьСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
		ОбластьИтого = Макет.ПолучитьОбласть("Итого");
		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
		
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
		// Массив для проверки вывода новой строки
		Строка = Новый Массив; 
		Строка.Добавить(ОбластьСтрокаТаблицы);
		
		// Массив для проверки вывода итогов с подвалом
		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(ОбластьСтрокаТаблицы);
		СтрокаСПодвалом.Добавить(ОбластьИтого); 
		СтрокаСПодвалом.Добавить(ОбластьПодвал); 
		
		// Вывод строк таблицы
		Пока Выборка.Следующий() Цикл
		
			ТекущаяСтрока = ТекущаяСтрока + 1;
			
			// Проверка на возможность вывода Строки без переноса на следующий лист
			Если Не ТабличныйДокумент.ПроверитьВывод(Строка) Тогда

				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
			
			КонецЕсли;
			
			// Проверка вывода последней строки
			Если ТекущаяСтрока = КоличествоСтрок И Не ТабличныйДокумент.ПроверитьВывод(СтрокаСПодвалом) Тогда
				
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
			
			КонецЕсли;
			
			ИтогоКоличество = ИтогоКоличество + ?(ЗначениеЗаполнено(Выборка.Количество), Выборка.Количество, 0);
			ОбластьСтрокаТаблицы.Параметры.Заполнить(Выборка);
			ОбластьСтрокаТаблицы.Параметры.НомерСтроки = ТекущаяСтрока;
			ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);
			
		КонецЦикла;
		
		ОбластьИтого.Параметры.ИтогоКоличество = ИтогоКоличество;
		ТабличныйДокумент.Вывести(ОбластьИтого);
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
	КонецЦикла;
	
	УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);
	
	Возврат ТабличныйДокумент;
	
КонецФункции
// }
